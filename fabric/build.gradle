plugins {
    id 'com.gradleup.shadow'
}

architectury {
    platformSetupLoomIde()
    fabric()
}

configurations {
    common {
        canBeResolved = true
        canBeConsumed = false
    }
    compileClasspath.extendsFrom common
    runtimeClasspath.extendsFrom common
    developmentFabric.extendsFrom common

    // Files in this configuration will be bundled into your mod using the Shadow plugin.
    // Don't use the `shadow` configuration from the plugin itself as it's meant for excluding files.
    shadowBundle {
        canBeResolved = true
        canBeConsumed = false
    }
}

dependencies {
    modImplementation "net.fabricmc:fabric-loader:$rootProject.fabric_loader_version"

    // Fabric API. This is technically optional, but you probably want it anyway.
    modImplementation "net.fabricmc.fabric-api:fabric-api:$rootProject.fabric_api_version"

    // Architectury API. This is optional, and you can comment it out if you don't need it.
    modImplementation "dev.architectury:architectury-fabric:$rootProject.architectury_api_version"

    common(project(path: ':common', configuration: 'namedElements')) { transitive = false }
    shadowBundle project(path: ':common', configuration: 'transformProductionFabric')

    modImplementation("dev.ftb.mods:ftb-library-fabric:${rootProject.ftb_library_version}") { transitive false }
    modImplementation("dev.ftb.mods:ftb-teams-fabric:${rootProject.ftb_teams_version}") { transitive false }
    modImplementation("dev.ftb.mods:ftb-quests-fabric:${rootProject.ftb_quests_version}") { transitive false }
    modCompileOnly("net.puffish:skillsmod:${puffish_skills}:fabric")
    modCompileOnly "curse.maven:blabber-565396:6445342"
    modCompileOnly "dev.onyxstudios.cardinal-components-api:cardinal-components-base:6.1.2"
    modCompileOnly "dev.onyxstudios.cardinal-components-api:cardinal-components-entity:6.1.2"

    modCompileOnly("maven.modrinth:spell-engine:1.8.9+1.21.1-fabric")

    modCompileOnly "curse.maven:levelz-517130:6181516"

    modCompileOnly("dev.muon.dynamic_difficulty:dynamic_difficulty-fabric:${dynamic_difficulty_version}")
    modCompileOnly "curse.maven:dungeon-difficulty-645559:7310940"

    modCompileOnly ("curse.maven:origins-391943:6112839"){
        transitive = false
    }
    modCompileOnly files("libs/Calio-1.14.0-alpha.9+mc.1.21.x.jar")

    modCompileOnly "curse.maven:easy-npc-core-1308987:7619174"
    modCompileOnly "curse.maven:easy-npc-config-ui-1214728:7618692"
}

processResources {
    inputs.property 'version', project.version

    filesMatching('fabric.mod.json') {
        expand version: inputs.properties.version
    }
}

shadowJar {
    configurations = [project.configurations.shadowBundle]
    archiveClassifier = 'dev-shadow'
}

remapJar {
    inputFile.set shadowJar.archiveFile
}
